apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

dependencies {
    implementation project(':vmr-annotation')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.lifecycleVersion"

    api 'com.squareup:kotlinpoet:1.5.0'
    implementation "com.google.auto.service:auto-service:1.0-rc6"
    kapt "com.google.auto.service:auto-service:1.0-rc6"
}

sourceCompatibility = "8"
targetCompatibility = "8"

ext {
    libraryName = 'VMR-Processor'
    artifact = 'vmr-processor'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            pom {
                name = libraryName
                description = libraryDescription
                url = siteUrl

                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }
                developers {
                    developer {
                        id = developerId
                        name = developerName
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://api.bintray.com/content/${bintraySubject}/${bintrayRepo}/${libraryName}/${libraryVersion}"

            credentials {
                username = properties.getProperty("bintray.user")
                password = properties.getProperty("bintray.apikey")
            }
        }
    }
}