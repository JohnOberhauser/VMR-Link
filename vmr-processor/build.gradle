apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
//apply plugin: 'com.github.dcendents.android-maven'

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    }
}

dependencies {
    implementation project(':vmr-annotation')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'com.squareup:kotlinpoet:1.5.0'
    implementation 'com.squareup:kotlinpoet-metadata:1.5.0'
    implementation "com.google.auto.service:auto-service:1.0-rc6"
    kapt "com.google.auto.service:auto-service:1.0-rc6"
}

sourceCompatibility = "8"
targetCompatibility = "8"

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

tasks.register("javadocJar", Jar) {
    from javadoc
    classifier "javadoc"
}

ext {
    bintrayRepo = 'Android-Libs'
    bintrayName = 'VMR-Processor'

    publishedGroupId = 'com.ober'
    libraryName = 'VMR-Processor'
    artifact = 'vmr-processor'

    libraryDescription = 'A link between your view model and your repository layer'

    siteUrl = 'https://github.com/JohnOberhauser/VMR-Link'
    gitUrl = 'https://github.com/JohnOberhauser/VMR-Link.git'

    libraryVersion = '3.0.0'

    developerId = 'JohnOberhauser'
    developerName = 'John Oberhauser'

    licenseName = 'GNU General Public License v3.0'
    licenseUrl = 'https://raw.githubusercontent.com/JohnOberhauser/VMR-Link/master/LICENSE'
    allLicenses = ["gpl-3.0"]
}

group = publishedGroupId
version = libraryVersion

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = libraryName
                description = libraryDescription
                url = siteUrl
                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }
                developers {
                    developer {
                        id = developerId
                        name = developerName
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['mavenJava']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}